
/**
Object to create row of text that can be used as element in linked list.
*/
class TextRow {
    
    field StringBuffer text;
    field TextRow next_row;
    field TextRow previous_row;


    /**
    Constructor: Constructs new row object that can be used in linked list.
    Parameters:
    */
    constructor TextRow new() {
        let text = StringBuffer.new(5);
        let next_row = null;
        let previous_row = null;
        return this;
    }


    /**
    Method: Disposes the object. Does not touch next or previous object.
    Parameters:
    */
    method void dispose() {
        do text.dispose();
        do Memory.deAlloc(this);
        return;
    }


    /**
    Method: Sets next_row to point the next object.
    Parameters:
            nr: Next TextRow object.
    */
    method void setNextRow(TextRow nr) {
        let next_row = nr;
        return;
    }


    /**
    Method: Sets previous_row to point the previous object.
    Parameters:
            pr: Previous TextRow object.
    */
     method void setPreviousRow(TextRow pr) {
        let previous_row = pr;
        return;
    }


    /**
    Method: Returns pointer to next TextRow object.
    Parameters:
    */
    method TextRow getNextRow() {
        return next_row;
    }


    /**
    Method: Returns pointer to previous TextRow object.
    Parameters:
    */
    method TextRow getPreviousRow() {
        return previous_row;
    }


    /**
    Method: Returns text content of the row.
    Parameters:
    */
    method StringBuffer getContents() {
        return text;
    }


    /** 
    Method: Append a character to the row. Character at index is overwritten.
    Parameters:
            c: Character that is appended.
            index: Position where character is added.
    */
    method void appendChar(char c, int index) {
        do text.appendChar(c, index);
        return;
    }


    /** 
    Method: Remove a character from the row.
    Parameters:
            index: Position where character is removed.
    */
    method void removeChar(int index) {
        do text.removeChar(index);
        return;
    }


    /** 
    Method: Append a row to the row. Character at index is not overwritten.
    Parameters:
            r: Row that is appended.
    */
    method void combineRows(TextRow r) {
        do text.appendSb(r.getContents(), text.length());
        let next_row = r.getNextRow();
        do r.dispose();
        return;
    }


    /**
    Method: Returns length of the row.
    Parameters:
    */
    method int length() {
        return text.length();
    }


    /**
    Method: Prints the object.
    Parameters:
    */
    method void print() {
        do text.print();
        return;
    }


    /**
    Function: Test function for TextRow class.
    */
    function boolean testTextRow() {
        var boolean pass;
        var TextRow r1;
        var TextRow r2;
        var TextRow r3;
        var TextRow temp;

        let pass = true;
        let r1 = TextRow.new();
        let r2 = TextRow.new();
        let r3 = TextRow.new();   
        
        do r1.appendChar(66, 0);
        do r1.appendChar(66, 1);
        do r1.appendChar(66, 2);
        do r1.appendChar(66, 3);
        do r1.appendChar(66, 4);
        do r1.print();
        
        do r2.appendChar(67, 0);
        do r2.appendChar(67, 1);
        do r2.appendChar(67, 2);
        do r2.appendChar(67, 3);
        do r2.appendChar(67, 4);
        do r2.print();

        do r3.appendChar(68, 0);
        do r3.appendChar(68, 1);
        do r3.appendChar(69, 2);
        do r3.appendChar(68, 3);
        do r3.appendChar(68, 4);
        do r3.print();

        // Test removing character from row.
        do r3.removeChar(2);
        do r3.print();

        // Test if adding next and previous rows work.
        //do r2.setPreviousRow(r1);
        do r2.setNextRow(r3);
        let temp = r2.getNextRow();
        do temp.print();

         // Test combining two rows.
        do r1.combineRows(r2);
        do r1.print();

        do r1.dispose();
        do r3.dispose();
        
        return pass;
    }
}